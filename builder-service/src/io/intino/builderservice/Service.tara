dsl Konos

Service builder-service as REST(host = "", port = "{port}")
	Resource(path = "/builders") builders
		Get > Response as Object(schema = BuilderInfo) List
		Post
			Parameter(in = body) builderInfo as Object(schema = BuilderInfo)
			Response(code ="201")

	Resource("/operations/run") runOperation
		Post
			Parameter(in = form) runOperationContext as Object(schema = RunOperationContext)
			Parameter(in = form) filesInTar as File
			Response ticket as Text
	Notification(path = "operation/:ticket/messages") subscribeOperation
		Parameter(in = path) ticket as Text
	Resource(path = "operation/:ticket/output") operationResult
		Get
			Parameter(in = path) ticket as Text
			Response operationResult as Object(OperationResult)
	Resource(path = "build/output/resource/:resourceId") resultResource
		Get
			Parameter(in = path) resourceId as Text
			Response file as File
			Exception(code = NotFound)

Schema OperationResult
	Attribute state as Word(values = "Running" "Finished")
	Attribute genTarHRef as Text
	Attribute srcTarHRef as Text
	Attribute resTarHRef as Text
	Attribute graphHRef as Text
	Attribute messages as Object(Message) List


Schema BuilderInfo
	Attribute id as Text
	Attribute imageName as Text
	Attribute operations as Text  List

Schema RunOperationContext
	Attribute builderId as Text
	Attribute operation as Text
	Attribute project as Text
	Attribute version as Text
	Attribute language as Text
	Attribute languageVersion as Text
	Attribute generationPackage as Text

Schema Message
	Attribute type as Text

Schema PresentableMessage as ExtensionOf(Message)
	Attribute type as Text("PresentableMessage")
	Attribute content as Text

Schema ExceptionMessage as ExtensionOf(Message)
	Attribute type as Text("ExceptionMessage")
	Attribute content as Text


Schema WarningMessage as ExtensionOf(Message)
	Attribute type as Text("WarningMessage")
	Attribute uri as Text
	Attribute line as Integer
	Attribute column as Integer
	Attribute content as Text

Schema SyntaxErrorMessage as ExtensionOf(Message)
	Attribute type as Text("SyntaxErrorMessage")
	Attribute uri as Text
	Attribute line as Integer
	Attribute column as Integer
	Attribute error as Text

Schema SemanticErrorMessage as ExtensionOf(Message)
	Attribute type as Text("SemanticErrorMessage")
	Attribute level as Word(values = "INFO" "WARNING" "RECOVERABLE_ERROR" "ERROR")
	Attribute uri as Text
	Attribute line as Integer
	Attribute column as Integer
	Attribute error as Text

