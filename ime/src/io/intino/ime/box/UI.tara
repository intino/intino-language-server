dsl Konos

use Theme

Service ImeElements as UI(port = "{port}", title = "{title}", favicon = "/icons/favicons/favicon.ico")
	Authentication(by="")

	Resource(path = "/") home as Page(homeTemplate) Main
	Resource(path = "/workspace/:name") workspace as Page(workspaceTemplate) Main

Template homeTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=headerTemplate) header

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingWorkspaceBlock
		Spinner(mode=Ring)
		Text(value="" , translate=true, format=h3 airTop) openingWorkspaceMessage

	Block(layout=Horizontal Flexible) bodyBlock
		Block(format=greyBackground) as Relative(width=20%)
			Block(format=autoHeight) as Relative(width=100%) > Block(layout=Vertical CenterCenter, format=leftPanelBackground topSpaced bottomSpaced) > Button(title="New workspace", highlight=Fill) as Action

			Block
				Selector workspacesGroupSelector as ListBox
					Text(value="All workspaces") allWorkspacesOption as Option
					Text(value="Your workspaces") ownerWorkspacesOption as Option
					Text(value="Shared with you") sharedWorkspacesOption as Option

		Block(layout=Vertical Flexible, format=spaced) > TemplateStamp(template=workspacesTemplate) workspacesCatalog

Template workspacesTemplate as Relative(height=100%, width=100%)
	Text(format=h4) title
	Table(itemClass="io.intino.ime.box.workspaces.Workspace") workspaceTable
		noItemsMessage = "No workspaces"
		Mold
			Heading workspaceTitleHeading > Sorting(collections=workspaceTable) titleSorting as OrderBy Labeled("Title")
			Item(height=30px, width=60%) workspaceTitleItem > Link(title="") title as Action
		Mold
			Heading workspaceOwnerHeading > Sorting(collections=workspaceTable) ownerSorting as OrderBy Labeled("Owner")
			Item(height=30px, width=20%) workspaceOwnerItem > Text owner
		Mold
			Heading lastModifiedDateHeading > Sorting(collections=workspaceTable) lastModifiedSorting as OrderBy Labeled("Last modified")
			Item(height=30px, width=10%) lastModifiedDateItem > Date lastModifiedDate
		Mold
			Heading operationsHeading > Text(value="Operations", translate=true)
			Item(height=30px, width=10%) operationsItem

Template workspaceTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=workspaceHeaderTemplate) header

	Block(layout=Horizontal Flexible)
		Block(layout=Vertical, format=greyLightBackground) as Relative(width=20%)
			Block(layout=Horizontal, format=headerPanel leftSpaced rightSpaced rightBordered)
				Block(layout=Horizontal Flexible) > Text(value="Browser", format=h6, translate=true)
				Block(layout=Horizontal EndJustified)
					MaterialIconButton(title="Add file", icon="Description", size=Small) addFilesTrigger as OpenDialog(addFilesDialog)
					MaterialIconButton(title="Add folder", icon="Folder", size=Small) addFolderTrigger as OpenDialog(addFolderDialog)
					MaterialIconButton(title="Remove", icon="Delete", size=Small) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly
					MaterialIconButton(title="Upload", icon="CloudUpload", size=Small) uploadFilesTrigger as OpenDialog(addFilesDialog)
			DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel) intinoFileBrowser

		Block(layout=Vertical Flexible)
			Block(layout=Horizontal, format=headerPanel leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
					MaterialIconButton(title="Save", icon="Save") saveFile as Action Readonly
			Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
				Text(value="Select file on left panel to open it")
			Block(layout=Vertical Flexible, visible=false) fileSelectedBlock
				DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor

	Dialog(title="Add file") addFilesDialog as Absolute(width=300px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text fileField as Editable(placeholder="Indicate file name") Labeled("File name") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addFilesDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFiles as Action Readonly

	Dialog(title="Add folder") addFolderDialog as Absolute(width=300px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text folderField as Editable(placeholder="Indicate folder name") Labeled("Folder name") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addFolderDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFolder as Action Readonly

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. If continue, you will loss changes in file. Are you want to continue?", translate=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileModifiedDialog)
            Button(title="Continue", size=Small, highlight=Fill) continueWithoutSavingFile as Action

Template(format=headerBackground topSpaced bottomSpaced) headerTemplate
	Block(layout=Horizontal, format=centered rightSpaced) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center, format=airLeft)
			Block
				Text(format=h3 appTitleStyle, value="Intino editor online")
				Text(format=whiteColor, value="Program your apps with your own DSL")
		Block(layout=Horizontal Center EndJustified)
			User(mode=OnlyPhoto) user

Template(format=headerBackground) workspaceHeaderTemplate
	Block(layout=Horizontal, format=centered rightSpaced) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center, format=airLeft)
			MaterialIconButton(title="Goto home", icon="Home", size=Large, color="white", format=airRight) as OpenPage(ImeElements.home)
			Block
				Text(format=h3 appTitleStyle, value="Workspace") title
				Text(format=whiteColor) description
		Block(layout=Horizontal Center EndJustified)
			User(mode=OnlyPhoto) user

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserItem) List
	Request open as Text

Schema intinoFileBrowserItem
	Attribute name as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute children as Text List

Display intinoDslEditor
	Notification refresh as Object(intinoDslEditorFile)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute workspace as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text
