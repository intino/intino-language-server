dsl Konos

use Editors
use UI
use Theme

Template workspacesTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=workspacesHeaderTemplate) header

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingWorkspaceBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingWorkspaceMessage

	Block(layout=Horizontal Flexible) bodyBlock
		Block(format=greyBackground) as Relative(width=20%)
			Block(format=autoHeight) as Relative(width=100%)
				Block(layout=Vertical CenterCenter, format=leftPanelBackground topSpaced bottomSpaced)
					Button(title="New workspace", highlight=Fill) as OpenDialog(addWorkspaceDialog)

			Block
				Selector workspacesGroupSelector as ListBox
					Text(value="All workspaces") allWorkspacesOption as Option
					Text(value="Private workspaces") privateWorkspacesOption as Option
					Text(value="Public workspaces") publicWorkspacesOption as Option

		Block(layout=Vertical Flexible, format=spaced) > TemplateStamp(template=workspacesCatalog) workspacesCatalog

	Dialog(title="New workspace") addWorkspaceDialog as Absolute(width=800px, height=800px)
        Block as Relative(height=100%, offsetHeight=40px)
			Text(format=doubleAirBottom) nameField as Editable Labeled("Name") Focused
			Text(format=doubleAirBottom) titleField as Editable Labeled("Title")
        	Block
        		Text(value="Language", translate=true, format=labelColor)
	        	Selector(format=doubleAirBottom) languageField as CollectionBox(placeholder="Select language")
	        		Table(itemClass="io.intino.ime.model.Language") languageTable as Selectable
	        			noItemsMessage = "No languages"
	        			Mold
	        				Heading ltNameHeading > Text(value="Name", translate=true)
	        				Item(height=20px, width=25%) ltNameItem > Text name
	        			Mold
	        				Heading ltVersionHeading > Text(value="Version", translate=true)
	        				Item(height=20px, width=25%) ltVersionItem > Text version
	        			Mold
	        				Heading ltOwnerHeading > Text(value="Owner", translate=true)
	        				Item(height=20px, width=50%) ltOwnerItem > Text owner
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addWorkspaceDialog)
            Button(title="Create", size=Small, highlight=Fill) createWorkspace as Action

Template workspacesCatalog as Relative(height=100%, width=100%)
	Text(format=h4) title
	Table(itemClass="io.intino.ime.model.Workspace") workspaceTable
		noItemsMessage = "No workspaces"
		Mold
			Heading workspaceTitleHeading > Sorting(collections=workspaceTable) titleSorting as OrderBy Labeled("Title")
			Item(height=30px, width=60%) workspaceTitleItem > Link(title="") title as Action
		Mold
			Heading workspaceOwnerHeading > Sorting(collections=workspaceTable) ownerSorting as OrderBy Labeled("Owner")
			Item(height=30px, width=20%) workspaceOwnerItem > Text owner
		Mold
			Heading lastModifiedDateHeading > Sorting(collections=workspaceTable) lastModifiedSorting as OrderBy Labeled("Last modified")
			Item(height=30px, width=10%) lastModifiedDateItem > Date lastModifiedDate
		Mold
			Heading operationsHeading > Text(value="Operations", translate=true)
			Item(height=30px, width=10%) operationsItem
				Block(layout=Horizontal)
					TemplateStamp(template=cloneWorkspaceEditor) cloneWorkspaceEditor
					MaterialIconButton(title="Remove workspace", icon="Delete", size=Small) removeWorkspace as Action Affirmed("Are you sure to remove workspace?")
					TemplateStamp(template=workspaceSettingsEditor) settingsEditor

Template(format=headerBackground middleTopSpaced middleBottomSpaced) workspacesHeaderTemplate
	Block(layout=Horizontal, format=centered rightSpaced) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center, format=airLeft)
			Link(title="", format=logoLink) as OpenPage(ImeElements.home)
			Image(value="/logo-dark.png", format=airLeft doubleAirRight, mobileReduceFactor=75%) as Absolute(width=45px, height=45px)
			Block
				Text(format=h3 appTitleStyle, value="My workspaces") title
				Text(format=whiteColor, value="The list of workspaces you designed")
			Block(layout=Horizontal, format=airLeft)
		Block(layout=Horizontal Center EndJustified)
			Block(format=airRight)
				MaterialIconButton(title="set dark mode", icon="Brightness4") as SetDarkMode
				MaterialIconButton(title="set light mode", icon="Brightness7") as SetLightMode
			User(mode=OnlyPhoto) user
				Selector as ListBox
					Block as Option > Link(title="Home") as OpenPage(page=ImeElements.home)