dsl Konos

use Theme

Template(layout=Vertical Flexible) languagesCatalog as Relative(height=100%, width=100%)
	Magazine(itemClass="io.intino.ime.model.Language") languagesMagazine
		noItemsMessage = "No languages"
		Mold
			Item(height=100px) languageMagazineItem
				Block(format=languageMagazine)
					Block(layout=Vertical Flexible)
						Block(layout=Horizontal)
							Block(layout=Vertical Flexible) > Text(format=orange) owner
							Block(layout=Horizontal EndJustified) > Text(visible=false) privatePill as Highlighted(textColor="white", backgroundColor="#777")
						Text(format=h5) title
					Block(layout=Horizontal Start, format=airTop)
						Block(layout=Vertical Flexible)
							Date(pattern="DD/MM/YYYY HH:mm") createDate as Labeled("Creation date")
							Text metaLanguage as Labeled("Created using")
						Block(layout=Vertical StartJustified)
							MaterialIconButton(title="Add model", icon="Add", size=Small, visible=false) addModel as Action
							MaterialIconButton(title="Add model", icon="Add", size=Small, visible=false) addPrivateModel as OpenDialog(empty)

	Dialog(title="Add model") addPrivateModelDialog as Absolute(height=360px, width=400px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Block(format=doubleAirBottom)
        		Text(value="Language", translate=true, format=labelColor)
        		Text languageField
        	Text(format=doubleAirBottom) nameField as Editable(helperText="Only alphanumeric characters are allowed") Labeled("Name")
        	Text(format=doubleAirBottom) titleField as Editable Labeled("Title") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addPrivateModelDialog)
            Button(title="Add", size=Small, highlight=Fill) createModel as Action

Template(layout=Vertical Flexible) languageVersionsCatalog as Relative(height=100%, width=100%)
	Toolbar
		Button(title="Create version...", size=Small, highlight=Outline) as OpenDialog(createVersionDialog)

	Table(itemClass="io.intino.ime.model.Language") languageVersionsTable as Selectable
		noItemsMessage = "No versions"
		Mold
			Heading lvtVersionHeading > Text(value="Version", translate=true)
			Item(height=30px, width=25%) lvtVersionItem > Text version
		Mold
			Heading lvtAccessTypeHeading > Text(value="Access", translate=true)
			Item(height=30px, width=25%) lvtAccessTypeItem > Text(format=airRight) accessType as Highlighted(textColor="white", backgroundColor="#aaa")
		Mold
			Heading lvtUpdateDateHeading > Text(value="Update date", translate=true)
			Item(height=30px, width=25%) lvtUpdateDateItem > Date updateDate
		Mold
			Heading lvtOperationsHeading > Text(value="Operations", translate=true)
			Item(height=30px, width=25%) lvtOperationsItem
				Block(layout=Horizontal)
					Link(title="Publish", format=airRight, visible=false) publishVersion as Action
					Link(title="Unpublish", format=airRight, visible=false) unpublishVersion as Action
					Link(title="Remove", format=airRight) removeVersion as Action Affirmed("Are you sure to remove version?")

	Dialog(title="Create version") createVersionDialog as Absolute(height=360px, width=400px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(format=doubleAirBottom) versionField as Editable(placeholder="1.0.0") Labeled("Version") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(createVersionDialog)
            Button(title="Create", size=Small, highlight=Fill) createVersion as Action
