dsl Konos

use Theme

Template(layout=Vertical Flexible) languagesCatalog as Relative(height=100%, width=100%)
	Magazine(itemClass="io.intino.ime.model.Language") languagesMagazine
		noItemsMessage = "No languages"
		Mold
			Item(height=100px) languageMagazineItem
				Block(format=languageMagazine)
					Block(layout=Vertical Flexible)
						Block(layout=Horizontal)
							Block(layout=Vertical Flexible) > Link(title="", format=orange) owner as Action
							Block(layout=Horizontal EndJustified) > Text(visible=false) privatePill as Highlighted(textColor="white", backgroundColor="#777")
						Text(format=h5) title
					Block(layout=Horizontal Start, format=airTop bottomSpaced)
						Block(layout=Vertical Flexible)
							Date(pattern="DD/MM/YYYY HH:mm") createDate as Labeled("Creation date")
							Text metaLanguage as Labeled("Created with")
							Number modelsCount as Labeled("Models count")
					Block(layout=Horizontal Wrap, format=topBordered topSpaced)
						Block(layout=Horizontal Flexible)
							Link(title="Versions", format=airRight) versionsDialogTrigger as OpenDialog(dialog=empty)
						Block(layout=Horizontal EndJustified)
							Link(title="Create model", size=Small, visible=false, format=airRight) addModel as Action
							Link(title="Create model", size=Small, visible=false, format=airRight) addPrivateModel as OpenDialog(empty)

	Dialog(title="Add model") addPrivateModelDialog as Absolute(height=360px, width=400px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Block(format=doubleAirBottom)
        		Text(value="Language", translate=true, format=labelColor)
        		Text languageField
        	Text(format=doubleAirBottom) nameField as Editable(helperText="Only alphanumeric characters are allowed") Labeled("Name")
        	Text(format=doubleAirBottom) titleField as Editable Labeled("Title") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addPrivateModelDialog)
            Button(title="Add", size=Small, highlight=Fill) createModel as Action

	Dialog(title="Versions") versionsDialog as Absolute(width=500px, height=600px)
        Block as Relative(height=100%, offsetHeight=40px)
        	TemplateStamp(template=languageAvailableVersionsCatalog) versionsCatalog
		Divider
		Block(layout=Horizontal EndJustified)
			Button(title="Close", size=Small, highlight=Outline) as CloseDialog(versionsDialog)

Template(layout=Vertical Flexible) languageAvailableVersionsCatalog as Relative(height=100%, width=100%)

	Table(itemClass="io.intino.ime.model.Language") languageAvailableVersionsTable
		noItemsMessage = "No versions"
		Mold
			Heading lavtVersionHeading > Text(value="Version", translate=true)
			Item(height=30px, width=30%) lavtVersionItem > Text version
		Mold
			Heading lavtUpdateDateHeading > Text(value="Update date", translate=true)
			Item(height=30px, width=20%) lavtUpdateDateItem > Date updateDate
		Mold
			Heading lavtOperationsHeading
			Item(height=30px, width=50%) lavtOperationsItem
				Block(layout=Horizontal EndJustified)
					Link(title="Create model", format=airRight) addModelWithVersion as Action