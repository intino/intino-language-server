dsl Konos

use UI
use Theme

Template(layout=Horizontal) languageEditor
	Block as Absolute(width=170px)
		Image(format=doubleAirBottom) logoField as Editable Absolute(height=150px, width=150px)
	Block(layout=Vertical Flexible)
		Text(format=doubleAirBottom) nameField as Editable(helperText="Only alphanumeric characters are allowed") Labeled("Name") Focused
		Text(format=doubleAirBottom) descriptionField as Editable Labeled("Title")
		Switch(title="Language is private", format=doubleAirBottom, state=On) accessTypeField

Template userSettingsEditor
	Block(format=doubleAirBottom) credentialsBlock
		Text(value="Credentials used in Quassar", translate=true, format=h5 airBottom)
		Block(layout=Vertical, format=doubleSpaced whiteBackground rounded airBottom)
			Image(format=airBottom, colorInvertedWithDarkMode=true) dockerHubImage as Absolute(width=120px, height=27px)
			Text(value="Go to your settings in Docker Hub to create a new personal access token for Quassar", translate=true, format=greyColor airBottom)
			Text dockerHubTokenField as Editable Password Labeled("Personal Access Token") Focused
		Block(layout=Vertical, format=doubleSpaced whiteBackground rounded airBottom)
			Block(layout=Horizontal Center)
				Image(format=airBottom, colorInvertedWithDarkMode=true) githubImage as Absolute(width=37px, height=37px)
				Text(value="github", format=airLeft h3)
			Text githubTokenField as Editable Password Labeled("Personal Access Token") Focused

Template languageSettingsEditor
	Selector(format=tabsStyle) tabSelector as Tabs
		Text(value="General", translate=true) propertiesOption as Option
		Text(value="Builder", translate=true) builderOption as Option

	Block(layout=Horizontal Flexible, format=doubleAirTop) propertiesBlock as Conditional(tabSelector.propertiesOption)
		Block as Absolute(width=170px)
			Image(format=doubleAirBottom) logoField as Editable Absolute(height=150px, width=150px)
		Block(layout=Vertical Flexible)
			Text(value="Name", translate=true, format=label)
			Text(format=doubleAirBottom) nameField
			Text(format=doubleAirBottom) descriptionField as Editable Labeled("Description") Focused
			Switch(title="Language is private", format=doubleAirBottom) accessTypeField
			Block
				Text(value="Tags", translate=true, format=label middleAirBottom)
				TemplateStamp(template=tagEditor) tags as Multiple("No tags", arrangement=Horizontal, spacing=DP8)
				Block(layout=Horizontal, format=airTop) > Button(title="Add tag", size=Small, highlight=Outline) as OpenDialog(addTagDialog)

	Block builderBlock as Conditional(tabSelector.builderOption)
		Block(format=airTop doubleAirBottom)
			Block(layout=Horizontal Center)
				Text builderField as Editable(placeholder="[username]/[builder]:[version]") Labeled("Docker image name")
				MaterialIcon(icon="CheckCircle", format=airTop greenColor, visible=false) validBuilderIcon
				MaterialIcon(icon="Help", format=airTop, visible=false) noBuilderInfoIcon
				MaterialIcon(icon="Cancel", format=airTop redColor, visible=false) invalidBuilderIcon
			Block(layout=Horizontal, format=airTop)
				Button(title="Check", highlight=Outline, size=Small, format=airRight) checkBuilder as Readonly
		Block(visible=false) loadingBlock
			Text(value="Loading builder information...", format=orange) builderState
		Block(visible=false) builderInfoBlock
			Block(format=doubleAirBottom) operationsBlock
				Text(value="Operations", format=h5, translate=true)
				TemplateStamp(template=operationEditor) operations as Multiple("No operations defined")
			Block(format=doubleAirBottom) builderDetailsBlock
				Text(value="Details", format=h5 airBottom, translate=true)
				Text(visible=false) errorMessage
				Block(visible=false) builderPropertiesBlock > TemplateStamp(template=builderPropertyView) builderProperties as Multiple(noItemsMessage="No information")

	Dialog(title="Add tag") addTagDialog as Absolute(width=280px, height=200px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text tagField as Editable(placeholder="Indicate tag name") Labeled("Tag") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addTagDialog)
            Button(title="Accept", size=Small, highlight=Fill) addTag as Action

Template(layout=Horizontal) modelSettingsEditor
	Button(title="Properties...", format=airRight, size=Small, visible=false) smallIconInList as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, visible=false) largeIconInList as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, size=Small, visible=false) smallIcon as OpenDialog(settingsDialog)
	Button(title="Properties...", format=airRight, visible=false) largeIcon as OpenDialog(settingsDialog)

	Dialog(title="Properties") settingsDialog as Relative(width=60%, height=80%) Modal
        Block as Relative(height=100%, offsetHeight=40px)
        	Block(format=airBottom)
        		Text(value="Attributes", translate=true, format=h5)
	        	Text(format=doubleAirBottom) settingsTitleField as Editable Labeled("Title")
        	Block(format=airBottom)
        		Text(value="Project access", translate=true, format=h5)
	        	Switch(title="Project is private", format=airBottom) accessTypeField
        	Block(format=airBottom)
        		Text(value="Remove project", translate=true, format=h5)
        		Text(value="Removed projects cannot be restored!", format=greyColor middleAirBottom)
				Block(layout=Horizontal) > Button(title="Remove project...", format=airRight redBackground whiteColor, highlight=Fill) removeModel as Action Readonly Affirmed("Are you sure to remove project?")
		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(settingsDialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action

Template(layout=Horizontal) cloneModelEditor
	Button(title="Clone...", format=airRight, size=Small, visible=false) smallIconInList as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, visible=false) largeIconInList as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, size=Small, visible=false) smallIcon as OpenDialog(modelDialog)
	Button(title="Clone...", format=airRight, visible=false) largeIcon as OpenDialog(modelDialog)

	Dialog(title="Clone project") modelDialog as Absolute(width=400px, height=260px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text modelTitleField as Editable(placeholder="Indicate title") Labeled("Title") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(modelDialog)
            Button(title="Accept", size=Small, highlight=Fill) cloneModel as Action

Template releaseEditor
	Block(format=doubleAirBottom)
		Block(layout=Vertical CenterCenter, format=doubleAirBottom)
			Text(value="New version number", format=middleAirBottom)
			Text(format=largeFontSize) version

		Selector versionTypeSelector as RadioBox
			Text(value="Major version") majorVersionOption as Option
			Text(value="Minor version") minorVersionOption as Option
			Text(value="Revision") revisionOption as Option

	Selector(format=doubleAirBottom) levelSelector as RadioBox Labeled("Language level")
		Text(value="Level 1") level1Option as Option
		Text(value="Level 2") level2Option as Option

Template(layout=Horizontal Center, format=airTop airBottom whiteBackground spaced rounded) operationEditor
	Block(format=airRight) as Absolute(width=200px)
		Text(value="Name", translate=true, format=label)
		Text nameField
	Block
		Text(value="Icon", translate=true, format=label airLeft)
		Block as Absolute(width=270px) > Selector typeField as ComboBox(view=TextView) Focused

	TemplateStamp(template=operationTypeView, visible=false)

Template(layout=Horizontal Center, format=tag) tagEditor
	Text(format=smallAirRight) nameField
	MaterialIconButton(title="Delete", icon="Clear", size=Small, format=smallIconSize) delete as Action

Template(layout=Horizontal Center) operationTypeView
	Block(layout=Vertical Flexible) > Text(format=middleAirRight) nameField
	Block(layout=Horizontal EndJustified) > MaterialIconButton(title="", icon="HelpOutline", size=Small) iconField as Action

Template(layout=Horizontal) builderPropertyView
	Block as Relative(width=25%) > Text name
	Block as Relative(width=75%) > Text value