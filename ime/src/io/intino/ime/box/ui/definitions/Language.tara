dsl Konos

use Models
use Search
use Languages
use Editors
use UI
use Theme

Template languageTemplate as Relative(height=100%, width=100%)
	Link(title="") openSearchLayerTrigger as OpenLayer(Fade, showHeader=false) Addressable(ImeElements.search)

	TemplateStamp(template=languageHeaderTemplate) header

	Block(layout=Vertical Flexible CenterCenter, visible=false) searchingModelsBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Vertical Flexible Center, format=greyBackground) bodyBlock as Relative(width=100%)

		Block(layout=Vertical CenterCenter Flexible, format=mainPanelStyle) as Relative(width=100%)
			Image(colorInvertedWithDarkMode=true) logo as Absolute(height=100px, width=100px)
			Block(layout=Horizontal Center, format=airTop)
				Text(format=h1 airRight) name
				Link(title="1.0.0", format=h1) release as OpenPopover(releasesBlock)
			Text(format=h3 greyColor) description
			Block(layout=Vertical Center, format=topSpaced)
				Button(title="Create language", size=Large, highlight=Fill, visible=false, format=largeButton airBottom) createLanguageTrigger as OpenDialog(createLanguageDialog)
				Button(title="Start modeling", size=Large, highlight=Fill, visible=false, format=largeButton airBottom) addModel as Action
				Button(title="Start modeling", size=Large, highlight=Fill, visible=false, format=largeButton airBottom) addPrivateModel as OpenDialog(addPrivateModelDialog)
				Link(title="View examples...", visible=false, format=airRight) viewModelExamples as OpenDialog(modelExamplesDialog)
				Link(title="View examples...", visible=false, format=airRight) viewLanguageExamples as OpenDialog(languagesExamplesDialog)

	Dialog(title="Models created with %s") modelExamplesDialog as Relative(width=90%, height=70%)
        Block as Relative(height=100%, offsetHeight=40px)
			TemplateStamp(template=modelsCatalog) modelsCatalog
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Close", size=Small, highlight=Fill) as CloseDialog(modelExamplesDialog)

	Dialog(title="Languages defined with %s") languagesExamplesDialog as Relative(width=90%, height=70%)
        Block as Relative(height=100%, offsetHeight=40px)
			TemplateStamp(template=languagesCatalog) languagesCatalog
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Close", size=Small, highlight=Fill) as CloseDialog(languagesExamplesDialog)

	Block(format=spaced) releasesBlock as Popover Absolute(width=500px, height=300px)
		TemplateStamp(template=languageReleasesCatalog) releasesCatalog

	Dialog(title="Create language") createLanguageDialog as Absolute(height=360px, width=600px)
        Block as Relative(height=100%, offsetHeight=40px)
			TemplateStamp(template=languageEditor) languageEditor
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(createLanguageDialog)
            Button(title="Create", size=Small, highlight=Fill) createLanguage as Action

	Dialog(title="Start modeling") addPrivateModelDialog as Absolute(height=360px, width=400px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Block(format=doubleAirBottom)
        		Text(value="Language", translate=true, format=labelColor)
        		Text languageField
        	Text(format=doubleAirBottom) titleField as Editable Labeled("Title") Focused
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(addPrivateModelDialog)
            Button(title="Start", size=Small, highlight=Fill) createModel as Action

Template(format=headerBackground) languageHeaderTemplate
	TemplateStamp(template=headerTemplate) quassarHeader
	Block(layout=Horizontal Wrap, format=centered rightSpaced middleTopSpaced middleBottomSpaced headerPanelBackground) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center Wrap, format=airLeft)
			Block(layout=Horizontal Center, format=airRight)
				Block(layout=Horizontal, format=airRight)
					Link(title="λ3", format=modelLink m3l3Style, visible=false) l3Link as OpenPage(ImeElements.model)
					Text(value="λ3", format=modelText m3l3Style, visible=false) l3
					Link(title="λ2", format=modelLink m2l2Style, visible=false) l2Link as OpenPage(ImeElements.model)
					Link(title="λ2", format=modelLink m2l2Style, visible=false) l2Children as OpenPopover(languageChildrenView)
					Text(value="λ2", format=modelText m2l2Style, visible=false) l2
					Link(title="λ1", format=modelLink m1l1Style, visible=false) l1Link as OpenPage(ImeElements.model)
					Link(title="λ1", format=modelLink m1l1Style, visible=false) l1Children as OpenPopover(languageChildrenView)
					Text(value="λ1", format=modelText m1l1Style, visible=false) l1
			Text(format=appTitleStyle) title
		Block(layout=Horizontal Center EndJustified, format=airLeft)
			Block(layout=Horizontal Center)
				Button(title="Open metamodel...", size=Small, format=airRight) openModel as Action
				Button(title="Properties...", size=Small) viewSettings as OpenDialog(dialog=settingsDialog)

	Block(format=spaced) languageChildrenView as Popover Absolute(width=800px, height=400px)
		TemplateStamp(template=languagesCatalogTable) languageChildrenCatalog

	Dialog(title="Settings") settingsDialog as Relative(width=60%, height=80%) Modal
        Block(format=overflowAuto) as Relative(height=100%, offsetHeight=40px)
        	TemplateStamp(template=languageSettingsEditor) settingsEditor
		Divider
        Block(layout=Horizontal)
        	Block(layout=Horizontal StartJustified Flexible) > Button(title="Close", size=Small, format=airRight) as CloseDialog(settingsDialog)
            Block(layout=Horizontal EndJustified) > Button(title="Apply", size=Small, highlight=Fill) saveSettings as Action
