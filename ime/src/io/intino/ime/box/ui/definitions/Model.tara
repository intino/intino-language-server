dsl Konos

use Models
use Languages
use Editors
use UI
use Theme

Template modelTemplate as Relative(height=100%, width=100%)
	Link(title="") openSearchLayerTrigger as OpenLayer(Fade, showHeader=false) Addressable(ImeElements.search)

	TemplateStamp(template=modelHeaderTemplate) header

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingModelBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Horizontal Flexible) bodyBlock
		Block(layout=Vertical, format=greyLightBackground) browserBlock as Relative(width=20%)
			Block(layout=Horizontal, format=headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible) > Text(value="Project", format=h6, translate=true)
				Block(layout=Horizontal, format=smallAirLeft)
					MaterialIconButton(title="Operations...", icon="MoreVert", size=Small) operationsTrigger as OpenPopover(workspaceDialog)
					// TODO MaterialIconButton(title="collapse", icon="Close") as CloseBlock()
			DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel) intinoFileBrowser

		Block(layout=Vertical Flexible)
			Block(layout=Horizontal, format=leftBordered headerPanel greyLightBackground leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
			Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
				Text(value="Select file on left panel to open it")
			Block(layout=Vertical Flexible, visible=false) fileSelectedBlock
				DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor
				TemplateStamp(template=consoleTemplate, visible=false) console

	Block(format=spaced) workspaceDialog as Popover Absolute(width=200px)
		Block(layout=Vertical Start)
			MaterialIconButton(title="Add file", icon="Description", titlePosition=Right, size=Small, format=middleAirBottom) addFileTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Control, visible=true)
			MaterialIconButton(title="Add folder", icon="Folder", titlePosition=Right, size=Small, format=middleAirBottom) addFolderTrigger as OpenDialog(fileDialog) > Shortcut(key="N", metaKeys=Shift Control, visible=true)
			MaterialIconButton(title="Rename", icon="Edit", titlePosition=Right, size=Small, format=middleAirBottom) editFilenameTrigger as OpenDialog(fileDialog) Readonly > Shortcut(key="R", metaKeys=Control, visible=true)
			MaterialIconButton(title="Remove", icon="Delete", titlePosition=Right, size=Small, format=middleAirBottom) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly

	Dialog(title="") fileDialog as Absolute(width=600px, height=350px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
        	Block(format=greyLightBackground) selectorBlock as Absolute(width=150px)
				Selector fileDialogSelector as ListBox
					Text(value="New file") newFileOption as Option
					Text(value="Upload file") uploadFileOption as Option
			Block(layout=Vertical Flexible, format=airLeft)
				Block(format=airTop) fileFieldBlock as Conditional(selectorBlock.fileDialogSelector.newFileOption)
					Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
				Block(format=airTop airBottom) fileUploadBlock as Conditional(selectorBlock.fileDialogSelector.uploadFileOption)
					File fileUploadField as Editable(showDropZone=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. What do you want to do?", translate=true)
		Divider
		Block(layout=Horizontal)
			Block(layout=Horizontal Flexible StartJustified)
				Button(title="Cancel", size=Small, format=airRight) cancelSavingFile as Action
			Block(layout=Horizontal EndJustified)
				Button(title="Don't save", size=Small, format=airRight) continueWithoutSavingFile as Action
				Button(title="Save", size=Small, highlight=Fill) continueSavingFile as Action

Template(format=headerBackground) modelHeaderTemplate
	TemplateStamp(template=headerTemplate) quassarHeader
	Block(layout=Horizontal Wrap, format=centered rightSpaced middleTopSpaced middleBottomSpaced headerPanelBackground) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center Wrap, format=airLeft)
			Block(layout=Horizontal Center, format=airRight)
				Block(layout=Horizontal, format=airRight)
					Link(title="M3", format=modelLink m3l3Style, visible=false) m3Link as OpenPage(ImeElements.model)
					Text(value="M3", format=modelText m3l3Style, visible=false) m3
					Link(title="M2", format=modelLink m2l2Style, visible=false) m2Link as OpenPage(ImeElements.model)
					Link(title="M2", format=modelLink m2l2Style, visible=false) m2Children as OpenPopover(modelChildrenView)
					Text(value="M2", format=modelText m2l2Style, visible=false) m2
					Link(title="M1", format=modelLink m1l1Style, visible=false) m1Link as OpenPage(ImeElements.model)
					Link(title="M1", format=modelLink m1l1Style, visible=false) m1Children as OpenPopover(modelChildrenView)
					Text(value="M1", format=modelText m1l1Style, visible=false) m1
				Link(format=appTitleStyle, title="Model") titleLink
				Block(layout=Horizontal Center, format=middleAirBottom, visible=false) titleEditor
					Text(value="Browser", format=h6 whiteBackground airRight minTitleEditorWidth) titleField as Editable
					Link(title="Cancel", format=airRight) cancel as Action
					Link(title="Save") save as Action
		Block(layout=Horizontal Center EndJustified, format=airLeft)
			Block(layout=Horizontal Center)
				Block(format=languageOperationsStyle relative, visible=false) modelOperationsBlock
					Text(value="", format=modelOperationsTextStyle, translate=true)
					TemplateStamp(template=modelOperation) operations as Multiple(noItemsMessage="", arrangement=Horizontal)
				Button(title="Publish...", format=iconButtonColor airRight, size=Small) publishTrigger as OpenDialog(publishDialog)
				Button(title="Open language...", size=Small, format=airRight) languagePath as OpenPage(ImeElements.language)
				TemplateStamp(template=cloneModelDialog) cloneModelEditor
				TemplateStamp(template=modelSettingsDialog) settingsEditor

	Block(format=spaced) modelChildrenView as Popover Absolute(width=800px, height=400px)
		TemplateStamp(template=modelsCatalog) modelChildrenCatalog

	Dialog(title="Publish") publishDialog as Absolute(width=300px, height=400px)
        Block as Relative(height=100%, offsetHeight=40px)
        	TemplateStamp(template=releaseEditor) releaseEditor
		Divider
		Block(layout=Horizontal EndJustified)
			Button(title="Cancel", size=Small, highlight=Outline, format=airRight) as CloseDialog(publishDialog)
			Button(title="Create", size=Small, highlight=Fill) publish as Action

Template modelOperation
	Button(title="", size=Small, format=airRight) trigger as Action

Template consoleTemplate
	Text(value="Soy los problems")

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserInfo)
	Notification select as Object(intinoFileBrowserItem)
	Request open as Text
	Request rename as Object(intinoFileBrowserRenameInfo)
	Request move as Object(intinoFileBrowserMoveInfo)

Schema intinoFileBrowserInfo
	Attribute itemAddress as Text
	Attribute items as Object(intinoFileBrowserItem) List

Schema intinoFileBrowserRenameInfo
	Attribute id as Integer
	Attribute newName as Text

Schema intinoFileBrowserMoveInfo
	Attribute file as Integer
	Attribute directory as Text

Schema intinoFileBrowserItem
	Attribute id as Integer
	Attribute name as Text
	Attribute uri as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute parents as Text List
	Attribute children as Text List

Display intinoDslEditor
	Notification refresh as Object(intinoDslEditorFile)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute model as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text
