dsl Konos

use Languages
use Editors
use UI
use Theme

Template modelTemplate as Relative(height=100%, width=100%)
	TemplateStamp(template=modelHeaderTemplate) header

	Block(layout=Vertical Flexible CenterCenter, visible=false) openingModelBlock
		Spinner(mode=Ring, color="secondary")
		Text(value="" , translate=true, format=h3 airTop) openingModelMessage

	Block(layout=Horizontal Flexible) bodyBlock
		Block(layout=Vertical, format=greyLightBackground) as Relative(width=20%)
			Block(layout=Horizontal, format=headerPanel leftSpaced rightSpaced rightBordered)
				Block(layout=Horizontal Flexible) > Text(value="Browser", format=h6, translate=true)
				Block(layout=Horizontal EndJustified)
					MaterialIconButton(title="Add file", icon="Description", size=Small) addFileTrigger as OpenDialog(fileDialog)
					MaterialIconButton(title="Clone file", icon="FileCopy", size=Small) copyFileTrigger as OpenDialog(fileDialog) Readonly
					MaterialIconButton(title="Add folder", icon="Folder", size=Small) addFolderTrigger as OpenDialog(fileDialog)
					MaterialIconButton(title="Edit name", icon="Edit", size=Small) editFilenameTrigger as OpenDialog(fileDialog) Readonly
					MaterialIconButton(title="Remove", icon="Delete", size=Small) removeFileTrigger as Action Affirmed("Are you sure to remove?") Readonly
			DisplayStamp(display=intinoFileBrowser, format=displayHeaderPanel) intinoFileBrowser

		Block(layout=Vertical Flexible)
			Block(layout=Horizontal, format=headerPanel leftSpaced rightSpaced)
				Block(layout=Horizontal Flexible)
					Text(value="", format=h6, translate=true) filename
					Block(layout=Horizontal, visible=false, format=airLeft) fileModifiedMessage > Text(value="modified", translate=true, format=airRight redColor bold)
					Block(layout=Horizontal, visible=false, format=airLeft) fileSavedMessage > Text(value="saved", translate=true, format=airRight greenColor bold)
				Block(layout=Horizontal EndJustified)
					MaterialIconButton(title="Save", icon="Save") saveFile as Action Readonly
			Block(layout=Vertical Flexible CenterCenter, format=whiteBackground) fileNotSelectedBlock
				Text(value="Select file on left panel to open it")
			Block(layout=Vertical Flexible, visible=false) fileSelectedBlock
				DisplayStamp(display=intinoDslEditor, format=fullHeight) intinoDslEditor

	Dialog(title="") fileDialog as Absolute(width=600px, height=350px)
        Block(layout=Horizontal) as Relative(height=100%, offsetHeight=40px)
        	Block(format=greyLightBackground) selectorBlock as Absolute(width=150px)
				Selector fileDialogSelector as ListBox
					Text(value="New file") newFileOption as Option
					Text(value="Upload file") uploadFileOption as Option
			Block(layout=Vertical Flexible, format=airLeft)
				Block(format=airTop) fileFieldBlock as Conditional(selectorBlock.fileDialogSelector.newFileOption)
					Text fileField as Editable(placeholder="Indicate name") Labeled("Name") Focused
				Block(format=airTop airBottom) fileUploadBlock as Conditional(selectorBlock.fileDialogSelector.uploadFileOption)
					File fileUploadField as Editable(showDropZone=true)
		Divider
        Block(layout=Horizontal EndJustified)
            Button(title="Cancel", size=Small, format=airRight) as CloseDialog(fileDialog)
            Button(title="Accept", size=Small, highlight=Fill) addFile as Action Readonly

	Dialog(title="File modified") fileModifiedDialog as Absolute(width=500px, height=180px)
        Block as Relative(height=100%, offsetHeight=40px)
        	Text(value="File have been modified. What do you want to do?", translate=true)
		Divider
		Block(layout=Horizontal)
			Block(layout=Horizontal Flexible StartJustified)
				Button(title="Cancel", size=Small, format=airRight) cancelSavingFile as Action
			Block(layout=Horizontal EndJustified)
				Button(title="Don't save", size=Small, format=airRight) continueWithoutSavingFile as Action
				Button(title="Save", size=Small, highlight=Fill) continueSavingFile as Action

Template(format=headerBackground middleTopSpaced middleBottomSpaced) modelHeaderTemplate
	Block(layout=Horizontal, format=centered rightSpaced) content as Relative(width=100%)
		Block(layout=Horizontal Flexible Center, format=airLeft)
			Link(title="", format=logoLink) as OpenPage(ImeElements.home)
			Image(value="/logo-dark.png", format=airLeft doubleAirRight, mobileReduceFactor=75%) as Absolute(width=45px, height=45px)
			Block
				Link(format=h3 appTitleStyle, title="Model") titleLink
				Block(layout=Horizontal Center, visible=false) titleEditor
					Text(value="Browser", format=h6 whiteBackground airRight minTitleEditorWidth) titleField as Editable
					Link(title="Cancel", format=airRight) cancel as Action
					Link(title="Save") save as Action
				Text(format=whiteColor) description
			Block(layout=Horizontal, format=airLeft)
				MaterialIconButton(title="Go model", icon="Dns", size=Medium, visible=false) models as OpenPage(ImeElements.models)
		Block(layout=Horizontal Center EndJustified)
			Block(layout=Horizontal, format=airRight)
				MaterialIconButton(title="Versions", icon="Category", visible=false) languageVersions as Action OpenDialog(dialog=versionsDialog)
				TemplateStamp(template=cloneModelEditor) cloneModelEditor
				MaterialIconButton(title="Remove model", icon="Delete", visible=false) removeModel as Action Affirmed("Are you sure to remove model?")
				TemplateStamp(template=modelSettingsEditor) settingsEditor
				MaterialIconButton(title="Set dark mode", icon="Brightness4") as SetDarkMode
				MaterialIconButton(title="Set light mode", icon="Brightness7") as SetLightMode
			User(mode=OnlyPhoto, visible=false) user
				Selector as ListBox
					Block as Option > Link(title="Home") as OpenPage(page=ImeElements.home)
					Block as Option > Link(title="My models") myModels as OpenPage(page=ImeElements.models)

	Dialog(title="Versions") versionsDialog as Relative(width=80%, height=70%)
        Block as Relative(height=100%, offsetHeight=40px)
        	TemplateStamp(template=languageVersionsCatalog) versionsCatalog
		Divider
		Block(layout=Horizontal EndJustified)
			Button(title="Close", size=Small, highlight=Outline) as CloseDialog(versionsDialog)

Display intinoFileBrowser
	Notification refresh as Object(intinoFileBrowserInfo)
	Notification select as Object(intinoFileBrowserItem)
	Request open as Text
	Request rename as Object(intinoFileBrowserRenameInfo)
	Request move as Object(intinoFileBrowserMoveInfo)

Schema intinoFileBrowserInfo
	Attribute itemAddress as Text
	Attribute items as Object(intinoFileBrowserItem) List

Schema intinoFileBrowserRenameInfo
	Attribute id as Integer
	Attribute newName as Text

Schema intinoFileBrowserMoveInfo
	Attribute file as Integer
	Attribute directory as Text

Schema intinoFileBrowserItem
	Attribute id as Integer
	Attribute name as Text
	Attribute type as Word("Folder" "File")
	Attribute isRoot as Bool
	Attribute parents as Text List
	Attribute children as Text List

Display intinoDslEditor
	Notification refresh as Object(intinoDslEditorFile)
	Notification receiveContent
	Request fileModified
	Request fileContent as Text

Schema intinoDslEditorFile
	Attribute name as Text
	Attribute model as Text
	Attribute uri as Text
	Attribute content as Text
	Attribute language as Text
	Attribute extension as Text
